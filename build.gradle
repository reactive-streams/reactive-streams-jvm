subprojects {
    apply plugin: "java"
    apply plugin: "osgi"

    group = "org.reactivestreams"
    version = "1.0.0.RC2"

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    tasks.withType(JavaCompile) {
        configure(options) {
            compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            encoding = "UTF-8"
        }
    }

    tasks.withType(Javadoc) {
        configure(options) {
            encoding "UTF-8"
            docEncoding "UTF-8"
            charSet "UTF-8"
            linkSource true
            noTimestamp true
        }
    }

    tasks.withType(Test) {
      testLogging {
        exceptionFormat "full"
        events "failed", "started"
      }
    }

    repositories {
        mavenCentral()
    }

    jar {
        manifest {
            instruction "Bundle-Vendor", "Reactive Streams SIG"
            instruction "Bundle-Description", "Reactive Streams API"
            instruction "Bundle-DocURL", "http://reactive-streams.org"
        }
    }

    if (name in ["reactive-streams", "reactive-streams-tck", "reactive-streams-examples"]) {
        apply plugin: "maven"
        apply plugin: "signing"

        signing {
            sign configurations.archives
            required { gradle.taskGraph.hasTask(uploadArchives) }
        }
        task sourcesJar(type: Jar) {
            classifier "sources"
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar) {
            classifier "javadoc"
            from javadoc
        }

        artifacts {
            archives sourcesJar, javadocJar
        }

        uploadArchives {
            repositories {
                mavenDeployer {
                    gradle.taskGraph.whenReady { taskGraph ->
                        if (taskGraph.hasTask(uploadArchives)) {
                            def userProp = "sonatypeOssUsername"
                            def passwordProp = "sonatypeOssPassword"
                            def user = project.properties[userProp]
                            def password = project.properties[passwordProp]

                            if (user == null || password == null) {
                                throw new InvalidUserDataException(
                                        "Cannot perform $uploadArchives.path due to missing credentials.\n" +
                                        "Run with command line args `-P$userProp=«username» -P$passwordProp=«password»` or add these properties to $gradle.gradleUserHomeDir/gradle.properties.\n")
                            }

                            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                                authentication(userName: user, password: password)
                            }
                            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                                authentication(userName: user, password: password)
                            }
                        }
                    }
                }
            }
        }

        tasks.withType(Upload) {
            repositories.withType(MavenResolver) {
                it.beforeDeployment { signing.signPom(it) }
                it.pom.whenConfigured { pom ->
                    pom.project {
                        url "http://www.reactive-streams.org/"
                        name "reactive-streams"
                        description "A Protocol for Asynchronous Non-Blocking Data Sequence"
                        inceptionYear "2014"

                        scm {
                            url "git@github.com:reactive-streams/reactive-streams.git"
                            connection "scm:git:git@github.com:reactive-streams/reactive-streams.git"
                        }

                        licenses {
                            license {
                                name "CC0"
                                url "http://creativecommons.org/publicdomain/zero/1.0/"
                                distribution "repo"
                            }
                        }

                        developers {
                            developer {
                                id "reactive-streams-sig"
                                name "Reactive Streams SIG"
                                url "http://www.reactive-streams.org/"
                            }
                        }
                    }
                }
            }
        }
    } else {
        uploadArchives.enabled = false
    }
}

import org.gradle.plugins.signing.Sign

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign } && signing.required) {
        // Use Java 6's console to read from the console (no good for
        // a CI environment)
        Console console = System.console()
        console.printf "\n\nWe have to sign some things in this build." +
                       "\n\nPlease enter your signing details.\n\n"

        def id = console.readLine("PGP Key Id: ")
        def file = console.readLine("PGP Secret Key Ring File (absolute path): ")
        def password = console.readPassword("PGP Private Key Password: ")

        allprojects { ext."signing.keyId" = id }
        allprojects { ext."signing.secretKeyRingFile" = file }
        allprojects { ext."signing.password" = password }

        console.printf "\nThanks.\n\n"
    }
}

