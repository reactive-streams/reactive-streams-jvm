description = 'reactive-streams-tck'

dependencies {
    api group: 'org.testng', name: 'testng', version:'7.3.0'
    api project(path: ':reactive-streams', configuration: 'mrjarArtifact')
    implementation project(':reactive-streams-examples')
}

jar {
    bundle {
        bnd ('Bundle-Name': 'reactive-streams-jvm',
                'Bundle-Vendor': 'Reactive Streams SIG',
                'Bundle-Description': 'Reactive Streams TCK',
                'Bundle-DocURL': 'http://reactive-streams.org',
                'Bundle-Version': project.version,
                'Export-Package': 'org.reactivestreams.tck.*',
                'Automatic-Module-Name': 'org.reactivestreams.tck',
                'Bundle-SymbolicName': 'org.reactivestreams.tck'
        )
    }
}

// Run the TCK baseline tests on LTS releases, JDK 8 and above.
def javaMinimumTarget = 8
def javaMaximumTarget = 21
def javaTestTargets = [javaMinimumTarget, 11, 17, javaMaximumTarget]
def jdk8Toolchain = JavaLanguageVersion.of(javaMinimumTarget)

compileTestJava {
    sourceCompatibility = 6
    targetCompatibility = 6

    javaCompiler = javaToolchains.compilerFor {
        languageVersion = jdk8Toolchain
    }
}

test {
    useTestNG()

    javaLauncher = javaToolchains.launcherFor {
        languageVersion = jdk8Toolchain
    }
}

javaTestTargets.each { majorVersion ->
    def jdkTest = tasks.register("testJdk$majorVersion", Test) {
        javaLauncher = javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(majorVersion)
        }

        description = "Runs the test suite on JDK $majorVersion"
        group = LifecycleBasePlugin.VERIFICATION_GROUP

        // Copy inputs from normal Test task.
        def testTask = tasks.getByName("test")

        useTestNG()
        if (majorVersion >= 9) {
            // tbd
            classpath = testTask.classpath
            testClassesDirs = testTask.testClassesDirs
        } else {
            classpath = testTask.classpath
            testClassesDirs = testTask.testClassesDirs
        }
    }
    tasks.named("check").configure { dependsOn(jdkTest) }
}
